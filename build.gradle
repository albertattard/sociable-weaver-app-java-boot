plugins {
    id "java"
    id "jacoco"
    id "org.sonarqube" version "3.3"
    id "io.freefair.lombok" version "6.2.0"
    id "info.solidsoft.pitest" version "1.7.0"
    id "org.springframework.boot" version "2.6.1"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

lombok {
    getVersion().set("1.18.22")
}

repositories {
    mavenCentral()
}

dependencies {
    /* Properties */
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    /* Spring Web */
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-websocket"

    /* Guava */
    implementation "com.google.guava:guava:31.0.1-jre"

    /* Testing */
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testRuntimeOnly "org.pitest:pitest-junit5-plugin:0.15"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

pitest {
    pitestVersion = "1.7.0"
    testPlugin = "junit5"
    targetClasses = ["aa.sw.*"]
    timestampedReports = false
    outputFormats = ["xml", "html"]
    mutationThreshold = 20
    coverageThreshold = 40
}

sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.projectName", "Sociable Weaver - Java/Boot Application"
    }
}
tasks.named("sonarqube").configure { dependsOn check }

check {
    dependsOn "pitest"
}
